import ip_discovery_common

/* Local agent.
 */

/* Per-LSP configuration received from central controller. */
input relation LSPConfig(
  lsp: lspid_t, // LSP identifier.
  tofu: bool)   // trust-on-first-use flag.

/* IP addresses claimed by VMs connected to local LSPs,
 * obtained using address snooping. */
input relation SnoopedAddress(
  lsp: lspid_t,      // LSP claiming the address.
  ip: ipaddr_t,      // Snooped address.
  timestamp: time_t) // Time address was first claimed.

/* Unique IP address per LSP selected from SnoopedAddress
 * based on LSP's tofu flag. */
output relation RealizedAddress(
  lsp: lspid_t,
  timestamp: time_t,
  ip: ipaddr_t)

/* No TOFU: use the most recent IP. */
RealizedAddress(lsp, newest.0, newest.1) :-
  LSPConfig(.lsp = lsp, .tofu = false),
  SnoopedAddress(lsp, ip, ts),
  var newest = Aggregate((lsp), group_max((ts, ip))).

/* TOFU: Use the oldest IP. */
RealizedAddress(lsp, oldest.0, oldest.1) :-
  LSPConfig(.lsp = lsp, .tofu = true),
  SnoopedAddress(lsp, ip, ts),
  var oldest = Aggregate((lsp), group_min((ts, ip))).
